import os
import snakePipes.common_functions as cf


### snakemake_workflows initialization ########################################
maindir = os.path.dirname(os.path.dirname(workflow.basedir))

# load conda ENVs (path is relative to "shared/rules" directory)
globals().update(cf.set_env_yamls())

# load config file
globals().update(cf.load_configfile(workflow.overwrite_configfile,config["verbose"]))
# load organism-specific data, i.e. genome indices, annotation, etc.
globals().update(cf.load_organism_data(genome,maindir,config["verbose"]))

# do workflow specific stuff now
include: os.path.join(workflow.basedir, "internals.snakefile")

### include modules of other snakefiles ########################################
################################################################################
# deeptools cmds
include: os.path.join(maindir, "shared", "tools" , "deeptools_cmds.snakefile")

# deepTools ChIP
include: os.path.join(maindir, "shared", "rules", "deepTools_ChIP.snakefile")

# MACS2 and MACS2 peak QC
include: os.path.join(maindir, "shared", "rules", "MACS2.snakefile")

# QC report for all samples
include: os.path.join(maindir, "shared", "rules", "ChiP-seq_qc_report.snakefile")

# sambamba
#include: os.path.join(maindir, "shared", "rules", "sambamba.snakefile")

# deeptools for allelic bams (if present)
if os.path.isdir(os.path.join(workingdir,'allelic_bams') ) and os.listdir(os.path.join(workingdir,'allelic_bams') ) != []:
    allele_info = 'TRUE'
    include: os.path.join(maindir, "shared", "rules", "deepTools_ChIP_allelic.snakefile")
else:
    allele_info = 'FALSE'

# histoneHMM (if mode is not allele-specific)
if allele_info == 'FALSE':
    include: os.path.join(maindir, "shared", "rules", "histoneHMM.snakefile")

def run_histoneHMM(allele_info):
    if allele_info == 'FALSE':
        ## run histoneHMM broad enrichment calling only for samples annotated as *broad*
        file_list = expand("histoneHMM/{chip_sample}.filtered.histoneHMM-regions.gff.gz", chip_sample = [s for s in chip_samples if is_broad(s)])
    else:
        file_list = []
    return(file_list)

# CSAW for differential binding (if sampleinfo specified)
if sample_info:
    include: os.path.join(maindir, "shared", "rules", "CSAW.snakefile")

def run_deepTools():
    file_list = []
    for chip_sample in chip_samples_w_ctrl:
        control_name = get_control_name(chip_sample)
        file_list.append([
        "deepTools_ChIP/bamCompare/"+chip_sample+".filtered.log2ratio.over_"+control_name+".bw",
        "deepTools_ChIP/bamCompare/"+chip_sample+".filtered.subtract."+control_name+".bw",
        "deepTools_ChIP/plotFingerprint/plotFingerprint.metrics.txt"
        ])
    return(file_list)

def run_deepTools_allelic():
    file_list = []
    if os.path.isdir('allelic_bams') and os.listdir('allelic_bams') != []:
        for chip_sample in chip_samples_w_ctrl:
            control_name = get_control_name(chip_sample)
            file_list.append([
            "deepTools_ChIP/bamCompare/allele_specific/"+chip_sample+".genome1.log2ratio.over_"+control_name+".bw",
            "deepTools_ChIP/bamCompare/allele_specific/"+chip_sample+".genome2.log2ratio.over_"+control_name+".bw",
            ])
        file_list.append( [
        "deepTools_ChIP/plotEnrichment/plotEnrichment.gene_features_allelic.png",
        "deepTools_ChIP/plotEnrichment/plotEnrichment.gene_features_allelic.tsv",
        "deepTools_ChIP/plotFingerprint/plotFingerprint.metrics_allelic.txt"
         ] )
    return(file_list)

def run_CSAW():
    if sample_info:
        return( ["CSAW/CSAW.session_info.txt"] )
    else:
        return([])

### execute before workflow starts #############################################
################################################################################
onstart:
    if "verbose" in config and config["verbose"]:
        print("--- Workflow parameter ---------------------------------------------------------")
        #print("All samples:", all_samples)
        print("control samples:", control_samples)
        print("ChIP samples w ctrl:", chip_samples_w_ctrl)
        print("ChIP samples wo ctrl:", chip_samples_wo_ctrl)
        print("paired:", paired)
        print("-" * 80, "\n")

        print("--- Environment ----------------------------------------------------------------")
        print("$TMPDIR: ",os.getenv('TMPDIR', ""))
        print("$HOSTNAME: ",os.getenv('HOSTNAME', ""))
        print("-" * 80, "\n")

        print("--- Genome ---------------------------------------------------------------------")
        print("Genome:", genome)
        print("Effective genome size:", genome_size)
        print("Genome FASTA:", genome_fasta)
        print("Genome index:", genome_index)
        print("Genome 2bit:", genome_2bit)
        print("Bowtie2 index:", bowtie2_index)
        print("Gene annotation BED:", genes_bed)
        print("Gene annotation GTF:", genes_gtf)
        print("Blacklist regions BED:", blacklist_bed)
        print("Ignore for normalization (bigwigs):", ignore_forNorm)
        print("-" * 80, "\n")

### main rule ##################################################################
################################################################################
rule all:
    input:
        run_deepTools(),
        expand("MACS2/{chip_sample}.filtered.BAM_peaks.xls", chip_sample = chip_samples),
        expand("MACS2/{chip_sample}.filtered.BAM_peaks.qc.txt", chip_sample = chip_samples),

        # run histoneHMM if allelic_bams are absent (since it gives index error without allele_specific index)
        run_histoneHMM(allele_info),
        ## run deeptools-allelic only if dir "allelic_bams" present and non empty
        run_deepTools_allelic(),
        ## run csaw if asked for
        run_CSAW(),
        "QC_report/QC_report_all.tsv"

### execute after workflow finished ############################################
################################################################################
onsuccess:
    cf.cleanLogs(outdir)
    if "verbose" in config and config["verbose"]:
        print("\n--- ChIP-seq workflow finished successfully! -----------------------------------\n")


## benchmark ###################################################################
## rule benchmark:
##     output: "benchmark/benchmark.tsv"
##for f in $(find . -name "*.benchmark" -type f); do seconds=$(cat $f | awk 'NR==2' | cut -f1); bname=$(basename $f | sed 's/.benchmark//'); echo -e "$bname\t$seconds" ; done
