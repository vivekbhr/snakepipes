import os
import snakePipes.common_functions as cf

### snakemake_workflows initialization ########################################
maindir = os.path.dirname(os.path.dirname(workflow.basedir))

# load conda ENVs (path is relative to "shared/rules" directory)
globals().update(cf.set_env_yamls())

# load config file
globals().update(cf.load_configfile(workflow.overwrite_configfile,config["verbose"]))
# load organism-specific data, i.e. genome indices, annotation, etc.
globals().update(cf.load_organism_data(genome,maindir,config["verbose"]))

# do workflow specific stuff now
include: os.path.join(workflow.basedir, "internals.snakefile")


### include modules of other snakefiles ########################################
################################################################################
include: os.path.join(maindir, "shared", "rules", "FASTQ.snakefile")
include: os.path.join(maindir, "shared", "rules", "RNA_mapping.snakefile")
include: os.path.join(maindir, "shared", "rules", "scRNAseq.snakefile")
fastqc = True
include: os.path.join(maindir, "shared", "rules", "FastQC.snakefile")
include: os.path.join(maindir, "shared", "rules", "filter_annotation.snakefile")
include: os.path.join(maindir, "shared", "tools" , "deeptools_cmds.snakefile")
include: os.path.join(maindir, "shared", "rules", "deepTools_RNA.snakefile")
paired = True
include: os.path.join(maindir, "shared", "rules", "multiQC.snakefile")
paired = False
include:os.path.join(maindir, "shared", "rules", "sambamba.snakefile")
include:os.path.join(maindir, "shared", "rules", "RNA-seq_qc_report.snakefile")

# TrimGalore
if trim:
    include: os.path.join(maindir, "shared", "rules", "TrimGalore.snakefile")

### conditional/optional rules #################################################
################################################################################

def run_Trimming(trim):
    if trim:
        file_list = [
        expand(fastq_dir+"/{sample}.fastq.gz", sample = samples),
        expand("FastQC_trimmed/{sample}_fastqc.html", sample = samples)
        ]
        return(file_list)
    else:
        return([])

def run_deeptools_qc():
    file_list = [
    expand("bamCoverage/{sample}.RPKM.bw", sample = samples),
    expand("bamCoverage/{sample}.coverage.bw", sample = samples),
    "deepTools_qc/plotEnrichment/plotEnrichment.tsv"]
    if len(samples)>1:
        file_list.append( ["deepTools_qc/multiBigwigSummary/coverage.bed.npz",
                           "deepTools_qc/plotCorrelation/correlation.pearson.bed_coverage.tsv",
                           "deepTools_qc/plotCorrelation/correlation.spearman.bed_coverage.tsv",
                           "deepTools_qc/plotPCA/PCA.bed_coverage.tsv"] )
    return(file_list)

### execute before workflow starts #############################################
################################################################################
onstart:
    if "verbose" in config and config["verbose"]:
        print("--- Workflow parameter ---------------------------------------------------------")
        print("Input directory:", indir)
        print("Input files:", infiles)
        print("Samples:", samples)
        print("Paired-end reads:", paired)
        print("Read extension:", reads)
        print("Genome:", genome)
        print("Downsample:", downsample)
        print("Trimming:", trim)
        print("Input directory for mapping:", fastq_dir)
        print("Input directory for trimming:", fastq_indir_trim)
        print("BigWig bin size:", bw_binsize)
        print("Barcode pattern:", barcode_pattern)
        print("Barcode file:", barcode_file)
        print("UMI_LEN:",UMI_length)
        print("UMI_offset:",UMI_offset)
        print("CELLI_LEN:",CELLI_length)
        print("CELLI_offset:",CELLI_offset)

        print("-" * 80, "\n")

        print("--- Environment ----------------------------------------------------------------")
        print("$TMPDIR: ",os.getenv('TMPDIR', ""))
        print("$HOSTNAME: ",os.getenv('HOSTNAME', ""))
        print("-" * 80, "\n")

### main rule ##################################################################
################################################################################
localrules: FASTQ
rule all:
    input:
        expand("FASTQ/{sample}{read}.fastq.gz", sample = samples, read = reads),
        expand("FASTQ_barcoded/{sample}.fastq.gz", sample = samples),
        run_Trimming(trim),
        expand("FastQC/{sample}{read}_fastqc.html", sample = samples, read=reads),
        expand(mapping_prg+"/{sample}.bam", sample = samples),
        expand(mapping_prg+"/{sample}.bam.bai", sample = samples),
        "Sambamba/flagstat_report_all.tsv",
        "Annotation/genes.filtered.bed",
        "Annotation/genes.filtered.gtf",
        "Annotation/genes.filtered.transcripts.gtf",      
        expand("Counts/{sample}.raw_counts.txt",sample = samples),
        expand("Counts/{sample}.featureCounts_summary.txt",sample = samples),
        expand("Counts/{sample}.corrected.txt",sample = samples),
        "Results/all_samples.gencode_genomic.corrected_merged.csv",
        "QC_report/QC_report.all_samples.libstats_reads.tsv",
        run_deeptools_qc(),
        "deepTools_qc/bamPEFragmentSize/fragmentSize.metric.tsv",
        expand("deepTools_qc/estimateReadFiltering/{sample}_filtering_estimation.txt",sample = samples),
        "multiQC/multiqc_report.html"


### execute after workflow finished ############################################
################################################################################
onsuccess:
    cf.cleanLogs(outdir)
    if "verbose" in config and config["verbose"]:
        print("\n--- scRNAseq-mapcount workflow finished successfully! --------------------------------\n")
